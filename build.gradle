buildscript {

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    minSdkVersion = 16
    targetSdkVersion = 27
    compileSdkVersion = 27
    buildToolsVersion = '26.0.2'

    supportLibVersion = '27.0.1'
    daggerVersion = '2.11'
    retrofitVersion = '2.1.0'
    hamcrestVersion = '1.3'

    supportLibAppCompat = "com.android.support:appcompat-v7:$supportLibVersion"
    supportCardView = "com.android.support:cardview-v7:$supportLibVersion"
    supportLibDesign = "com.android.support:design:$supportLibVersion"

    hamcrest = "org.hamcrest:hamcrest-all:$hamcrestVersion"
    junitVersion = '4.12'
    testRunnerVersion = '1.0.1'
    espressoCoreVersion = '3.0.1'
    espressoContribVersion = '2.2.2'

    junit = "junit:junit:$junitVersion"
    testRunner = "com.android.support.test:runner:$testRunnerVersion"
    espressoCore = "com.android.support.test.espresso:espresso-core:$espressoCoreVersion"
    espressoContrib = "com.android.support.test.espresso:espresso-contrib:$espressoContribVersion"
    espressoIdling = "com.android.support.test.espresso:espresso-idling-resource:$espressoCoreVersion"
    espressoIntent = "com.android.support.test.espresso:espresso-intents:$espressoCoreVersion"
    supportAnnotations = "com.android.support:support-annotations:$supportLibVersion"
}

/**
 * Launches the GCE local development server
 * Runs all tests
 * Shuts the server down again
 */
task TestWithGCETask(dependsOn: ":backend:appengineRun") {
    group "Other"
    description "Launches the GCE local development server\n" + "Runs all tests\n" +
        "Shuts the server down again"

    project(":backend").afterEvaluate { backend ->
        backend.extensions.appengine.daemon = true
    }

    finalizedBy(":app:connectedAndroidTest").finalizedBy(":backend:appengineStop")
}